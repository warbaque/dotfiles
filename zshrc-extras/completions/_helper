#compdef helper

_helper() {
  local ret=1
  local state line

  local cmds; cmds=(
    'minimal:minimal setup (zsh, dotfiles)'
    'extra:install extra packages'
    'gui:install gui packages'
    'full:install all'
    'no-gui:install full --no-gui'
    'tools:setup tools'
    'dotfiles:setup dotfiles'
    'sudoers:setup sudoers'
  )

  local dotfiles; dotfiles=(
    'zsh[zsh]'
    'tmux.conf[tmux]'
    'vimrc[vim]'
    'gitconfig[git]'
    'config/terminator[terminator]'
  )

  _arguments -C \
    '1: :->cmds' \
    '*::arg:->args' \
    && ret=0

  case "${state}" in
    (cmds) _describe -V 'command' cmds ;;
    (args)
      case "${line[1]}" in
        full) _arguments '1:arguments:--no-gui' ;;
        tools) _arguments '1:available_tools:_path_files -W "${HOME}/dotfiles/setup/tools" -g "*(.)"' "::install_tool:($(_helper_list_tools "${line[2]}"))" ;;
        dotfiles) _values 'description' "${dotfiles[@]}" ;;
      esac
    ;;
  esac
  return ret
}

(( $+functions[_helper_list_tools] )) ||
_helper_list_tools() {
  local cmd="${HOME}/dotfiles/setup/tools/${1}"
  [ -f "${cmd}" ] && "${cmd}" _commands
}

_helper "${@}"
