#compdef helper

_helper() {
  local ret=1
  local state line

  local -a cmds dotfiles
  cmds=(
    'minimal:minimal setup (zsh, dotfiles)'
    'extra:install extra packages'
    'gui:install gui packages'
    'full:install all'
    'no-gui:install full --no-gui'
    'tools:setup tools'
    'dotfiles:setup dotfiles'
    'sudoers:setup sudoers'
  )
  dotfiles=(
    'zsh[zsh]'
    'tmux.conf[tmux]'
    'vimrc[vim]'
    'gitconfig[git]'
    'config/terminator[terminator]'
  )

  _arguments -C \
    '1: :->cmds' \
    '*::arg:->args' \
    && ret=0

  case "${state}" in
    (cmds) _describe 'command' cmds ;;
    (args)
      case "${line[1]}" in
        full) _arguments '1:arguments:--no-gui' ;;
        tools) _arguments '1:available_tools:_path_files -W "${HOME}/dotfiles/setup/tools" -g "*(.)"' ;;
        dotfiles) _values -w 'description' "${dotfiles[@]}" ;;
      esac
    ;;
  esac
}

_helper "${@}"
